plugins {
    id 'java'
}

group 'org.shijinglu'
version '0.1.0'

sourceCompatibility = 1.8

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.1"
    implementation "org.antlr:antlr4-runtime:4.7.1"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'
}

generateGrammarSource {
    maxHeapSize = "64m"

    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java"
        }
    }
}

clean.doLast {
    file('generated-src').deleteDir()
}

// Publish to Maven Repository
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
        maven(MavenPublication) {
            groupId = 'org.shijinglu.lure'
            artifactId = 'lure-java'
            version = "${version}"
            from components.java
            pom {
                name = 'Lu\' Rule Engine'
                description = 'A small DSL lib that handles rule evaluation'
                developers {
                    developer {
                        id = 'shijinglu'
                        name = 'Shijing Lu'
                        email = 'Shijing.Lu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/shijinglu/lure.java.git'
                    developerConnection = 'scm:git:ssh://github.com/shijinglu/lure.java.git'
                    url = 'https://github.com/shijinglu/lure.java'
                }
            }
        }

    }
}
